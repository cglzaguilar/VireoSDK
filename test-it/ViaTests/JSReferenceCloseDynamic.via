define(DynamicReferencesVI dv(VirtualInstrument (
    Locals: c(
        ce(dv(.ErrorCluster (true 100 'problem' )) errorIn)
        e(.ErrorCluster err)
        e(.ErrorCluster getObjectReferenceError1)
        e(.ErrorCluster getObjectReferenceError2)

        e(.ErrorCluster noErrorInAndValidReferenceError)
        e(.ErrorCluster errorInAndValidReferenceError)
        e(.ErrorCluster noErrorInAndInvalidReferenceError)
        e(.ErrorCluster errorInAndInvalidReferenceError)

        e(.JavaScriptDynamicRefNum objectReference)
        e(dv(.Boolean false) validRefnum1)
        e(dv(.Boolean false) validRefnum2)
        e(dv(.Boolean false) isNotAValidRefnum1)
        e(dv(.Boolean false) isNotAValidRefnum2)
        e(dv(.Boolean false) isNotAValidRefnum3)
        e(dv(.Boolean false) isNotAValidRefnum4)
        e(.Occurrence occurrence)
    )
    clump (
        // no error in, valid reference, close reference => no error out, reference closed
        JavaScriptInvoke(occurrence false getObjectReferenceError1 'Date.now' objectReference)
        IsNotANumPathRefnum(objectReference validRefnum1)
        Not(validRefnum1 validRefnum1)
        CloseReference(objectReference noErrorInAndValidReferenceError)
        IsNotANumPathRefnum(objectReference isNotAValidRefnum1)

        // error in, valid reference, close reference => error out same as error in, reference closed
        JavaScriptInvoke(occurrence false getObjectReferenceError2 'Date.now' objectReference)
        IsNotANumPathRefnum(objectReference validRefnum2)
        Not(validRefnum2 validRefnum2)
        Copy(errorIn errorInAndValidReferenceError)
        CloseReference(objectReference errorInAndValidReferenceError)
        IsNotANumPathRefnum(objectReference isNotAValidRefnum2)

        // no error in, invalid reference, close reference => invalid reference error
        CloseReference(objectReference noErrorInAndInvalidReferenceError)
        IsNotANumPathRefnum(objectReference isNotAValidRefnum3)

        // error in, invalid reference, close reference => error out same as error in
        Copy(errorIn errorInAndInvalidReferenceError)
        CloseReference(objectReference errorInAndInvalidReferenceError)
        IsNotANumPathRefnum(objectReference isNotAValidRefnum4)

        Printf ("getObjectReferenceError1: %z\n" getObjectReferenceError1)
        Printf ("validRefnum1: %z\n" validRefnum1)
        Printf ("noErrorInAndValidReferenceError: %z\n" noErrorInAndValidReferenceError)
        Printf ("isNotAValidRefnum1: %z\n" isNotAValidRefnum1)
        Printf ("\n")
        Printf ("getObjectReferenceError2: %z\n" getObjectReferenceError2)
        Printf ("validRefnum2: %z\n" validRefnum2)
        Printf ("errorInAndValidReferenceError: %z\n" errorInAndValidReferenceError)
        Printf ("isNotAValidRefnum2: %z\n" isNotAValidRefnum2)
        Printf ("\n")
        Printf ("noErrorInAndInvalidReferenceError.status: %z\n" noErrorInAndInvalidReferenceError.status)
        Printf ("noErrorInAndInvalidReferenceError.code: %z\n" noErrorInAndInvalidReferenceError.code)
        Printf ("isNotAValidRefnum3: %z\n" isNotAValidRefnum3)
        Printf ("\n")
        Printf ("errorInAndInvalidReferenceError: %z\n" errorInAndInvalidReferenceError)
        Printf ("isNotAValidRefnum4: %z\n" isNotAValidRefnum4)
    )
) ) )
enqueue(DynamicReferencesVI)
